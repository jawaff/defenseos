plugins {
    id 'org.springframework.boot' version '3.4.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'com.dzyne.defenseos'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            // Fails builds when version conflicts are encountered (includes transitive dependencies).
            // Multiple different versions of the same dependency will cause version conflicts (i.e. group and name are equal).
            failOnVersionConflict()
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // Turn on all warnings and fail the build if there is a warning.
        // We unfortunately have to suppress the "requires directive for an automatic module"
        // warning because guava is an automatic module.
        // Processing warnings are turned off because of Junit annotations not being claimed by
        // an annotation processor.
        options.compilerArgs << '-Xlint:all,-processing' << '-Werror' << '-Xlint:-requires-automatic'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation 'org.assertj:assertj-core:3.27.0'

        constraints {
            annotationProcessor 'org.projectlombok:lombok:1.18.36'
            compileOnly 'org.projectlombok:lombok:1.18.36'
            testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
            testCompileOnly 'org.projectlombok:lombok:1.18.36'
            implementation 'com.google.guava:guava:33.4.0-jre'
        }
    }

    jacoco {
        toolVersion = "0.8.12"
        reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()

        testLogging.showStandardStreams = true

        finalizedBy jacocoTestReport
    }

    jacocoTestReport.dependsOn test
}